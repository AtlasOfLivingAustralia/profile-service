buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven {
            url "https://nexus.ala.org.au/content/groups/public/"
        }
        mavenCentral()

    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.6"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.3.4"
    }
}

version "$profileServiceVersion"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply from: "${project.projectDir}/gradle/publish.gradle"

def withoutXerces = { exclude group: 'xerces', module: 'xercesImpl' }

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven {
        url "https://nexus.ala.org.au/content/groups/public/"
    }
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

def esVersion = '7.8.1'
ext {
    elasticsearchVersion = esVersion
    elasticsearchAttachmentsVersion = esVersion
}

ext['elasticsearch.version'] = esVersion


springBoot {
    buildInfo()
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails:grails-async-gpars"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:gsp"
    profile "org.grails.profiles:web"

    // check for java 11
    implementation "commons-fileupload:commons-fileupload:1.3.1"

    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.3.4"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-api:4.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-support:4.0.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:4.0.0"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:4.0.0"

    // app specific dependencies
    implementation "org.grails.plugins:ala-auth:5.2.0-SNAPSHOT"
    implementation "au.org.ala:userdetails-service-client:2.0.0"
    implementation "org.grails.plugins:ala-ws-security-plugin:4.3.5-SNAPSHOT"
    implementation ("org.grails.plugins:ala-ws-plugin:3.1.2") {
        exclude module: 'asset-pipeline-grails'
        exclude module: 'xerces'
    }

    implementation "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"

    implementation ('au.org.ala:ala-name-matching-search:4.2')
    implementation ('au.org.ala:ala-name-matching:4.2')
    implementation "com.xlson.groovycsv:groovycsv:1.0"
    implementation "com.google.guava:guava:19.0"
    implementation ("com.google.apis:google-api-services-analytics:v3-rev116-1.20.0") {
        exclude module: 'guava-jdk5'
    }
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20160827.1'
    implementation "com.itextpdf:itextpdf:5.5.1"
    implementation "org.imgscalr:imgscalr-lib:4.2"
    implementation "com.google.code.gson:gson:2.8.6"

    implementation ("org.grails:grails-plugin-datasource")
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-codecs"
    implementation ("org.grails.plugins:mongodb:$gormMongoVersion")
    implementation "org.mongodb:mongodb-driver-sync:$mongodbDriverVersion"
    implementation "org.mongodb:bson:$mongodbDriverVersion"
    implementation "org.grails.plugins:mail:3.0.0"
    implementation "xml-apis:xml-apis:1.4.01"

    implementation "org.elasticsearch.client:elasticsearch-rest-high-level-client:${esVersion}"
    if (!Boolean.valueOf(inplace)) {
        implementation 'org.grails.plugins:elasticsearch:3.0.0.M1.ALA'
    }

    implementation "org.grails.plugins:dropwizard-metrics:2.0.0.RC1"
    implementation 'dk.glasius:external-config:3.0.0'
    implementation "org.quartz-scheduler:quartz:2.3.2"
    implementation "org.grails.plugins:quartz:2.0.13"

    // override log4j in elasticsearch plugin
    // TODO remove this once no log4j vulnerable transitive deps are used.
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.17.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-api:2.17.0'
}

configurations.all {
    exclude module:'grails-datastore-simple'
}

bootRun {
    ignoreExitValue true
    jvmArgs('-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

bootJar {
    enabled = true
    classifier = 'exec'
    launchScript()
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

webdriverBinaries {
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        chromedriver {
            version = '2.45.0'
            fallbackTo32Bit = true
        }
        geckodriver '0.30.0'
    }
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    if (!System.getenv().containsKey('GITHUB_ACTIONS')) {
        systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
        systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
    } else {
        systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
        systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
    }
}


assets {
    minifyJs = true
    minifyCss = true
}

if (Boolean.valueOf(inplace)) {
    grails {
        exploded = true
        plugins {
            implementation(project(':elasticsearch'))
        }
    }
}

